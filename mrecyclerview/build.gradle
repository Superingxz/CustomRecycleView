apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "1.0.0" // 修改为你的版本号

def siteUrl = 'https://github.com/Superingxz/CustomRecycleView' // 修改为你的项目的主页
def gitUrl = 'https://github.com/Superingxz/CustomRecycleView.git' // 修改为你的Git仓库的url

group = "com.myz.mrecyclerview" // Maven Group ID for the artifact，一般填你唯一的包名

install {

    repositories.mavenInstaller {

// This generates POM.xml with proper parameters

        pom {

            project {

                packaging 'aar'

// Add your description here

                name 'Jcenter for Android.' //项目描述

                url siteUrl

// Set your license

                licenses {

                    license {

                        name 'The Apache Software License, Version 2.0'

                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'

                    }

                }

                developers {

                    developer {

                        id 'Mo' //填写的一些基本信息

                        name 'yaozhi'

                        email '1803117759@qq.com'

                    }

                }

                scm {

                    connection gitUrl

                    developerConnection gitUrl

                    url siteUrl

                }

            }

        }

    }

}
android.libraryVariants.all { variant ->

    println variant.javaCompile.classpath.files

    if(variant.name == 'release') { //我们只需 release 的 javadoc

        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {

            // title = ''

            // description = ''

            source = variant.javaCompile.source

            classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())

            options {

                encoding "utf-8"

                links "http://docs.oracle.com/javase/7/docs/api/"

                linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"

            }

            exclude '**/BuildConfig.java'

            exclude '**/R.java'

        }

        task("javadoc${variant.name.capitalize()}Jar", type: Jar, dependsOn: "generate${variant.name.capitalize()}Javadoc") {

            classifier = 'javadoc'

            from tasks.getByName("generate${variant.name.capitalize()}Javadoc").destinationDir

        }

        artifacts {

            archives tasks.getByName("javadoc${variant.name.capitalize()}Jar")

        }

    }

}
task sourcesJar(type: Jar) {

    from android.sourceSets.main.java.srcDirs

    classifier = 'sources'

}

task javadoc(type: Javadoc) {

    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    classifier = 'javadoc'

    from javadoc.destinationDir

}

artifacts {

    archives javadocJar

    archives sourcesJar

}

Properties properties = new Properties()

properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {

    user = properties.getProperty("bintray.user")

    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    pkg {
        userOrg = "morlige" //这个是你创建时的organization,必须要有，不然没办法找到指定路径

        repo = "maven"

        name = "mrecyclerview" //发布到JCenter上的项目名字

        websiteUrl = siteUrl

        vcsUrl = gitUrl

        licenses = ["Apache-2.0"]

        publish = true

    }

}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
}
